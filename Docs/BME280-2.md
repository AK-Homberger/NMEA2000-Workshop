# BME280 Teil2 - Luftfeuchtigkeit

Mit dem zweiten Beispielprogramm zum BME280 werden wir nun auch die Luftfeuchtigkeit messen und als zusätzlichen PGN senden.

Hierbei werden wir exemplarisch alle Schritte durchgehen, die zur Erweiterung des Programms notwendig sind. Danach solltet ihr auch selbst in der Lage sein, eigenständige Erweiterungen vorzunehmen. 

Als erstes müssen wir einen eigenen Offset für das Senden der Luftfeuchtigkeit definieren:

```
#define SlowDataUpdatePeriod 1000  // Time between CAN Messages sent
#define TempSendOffset 0           // + 0 ms
#define HumiditySendOffset 50      // + 50 ms
````

Dazu fügen wir die Zeile: "#define HumiditySendOffset 50      // + 50 ms" hinzu.
Das bedeutet, die Luftfeuchtigkeit wird einmal pro Sekunde und 50 ms nach der Temperatur gesendet.

Wir haben uns entschieden, die Luftfeuchtigkeit mit dem PGN 130313 zu senden. Also fügen wir der Liste im Programm die Zeile "130313L, // Humidity" hinzu.
Das sieht dann so aus:

```
const unsigned long TransmitMessages[] PROGMEM = {130312L, // Temperature
                                                  130313L, // Humidity
                                                  0
                                                 };
````

Nun fügen wir die eigentliche Funktion zum Zusammenbauen und Senden der neuen Nachricht hinzu (nach der Funktion SendN2kTemperature())

```
void SendN2kHumidity(void) {
  static unsigned long SlowDataUpdated = InitNextUpdate(SlowDataUpdatePeriod, HumiditySendOffset);
  tN2kMsg N2kMsg;
  double Humidity;        

  if ( IsTimeToUpdate(SlowDataUpdated) ) {
    SetNextUpdate(SlowDataUpdated, SlowDataUpdatePeriod);
        
    Humidity = bme.readHumidity();
    Serial.printf("Humidity: %3.1f °C \n", Humidity);

    // Definition from N2kMessages.h
    // SetN2kPGN130313(tN2kMsg &N2kMsg, unsigned char SID, unsigned char HumidityInstance,
    //                 tN2kHumiditySource HumiditySource, double ActualHumidity, double SetHumidity=N2kDoubleNA);
    
    // HumiditySource is defined in N2kTypes.h

    // Set N2K message
    SetN2kPGN130313(N2kMsg, 0, 0, N2khs_InsideHumidity, Humidity, N2kDoubleNA);
    
    // Send message
    NMEA2000.SendMsg(N2kMsg);
  }
}
```

Der Aufbau der Funktion ist fast identisch zur Temperatur-Funktion. Nur initialisieren wir hier bei der Variablen "static unsigned long SlowDataUpdated" mit der zuvor definierten Konstanten "HumiditySendOffset";

Statt der Temperatur definieren wir eine lokale Variable "Humidity" und mit "Humidity = bme.readHumidity();" lesen wir den Wert der Luftfeuchtigkeit vom BME280-Modul und speichern den Wert in der Variablen. Der Wert ist in %.

Mit "SetN2kPGN130313(N2kMsg, 0, 0, N2khs_InsideHumidity, Humidity, N2kDoubleNA);" setzen wir die notwendigen Argumente für die Funktion. Mit "N2khs_InsideHumidity" setzen wir hier den Typ der Luftfeuchtigkeit (innen).

Und mit NMEA2000.SendMsg(N2kMsg) senden wir die Nachricht an den NMEA2000-Bus.

Nun sind wir fast fertig.

In loop() fügen wir nur die Zeile zum Senden der Luftfeuchtigkeit hinzu: SendN2kHumidity(). Das ist alles.

```
void loop() {

  SendN2kTemperature();
  
  SendN2kHumidity();

  NMEA2000.ParseMessages();

  CheckSourceAddressChange();
  
  // Dummy to empty input buffer to avoid board to stuck with e.g. NMEA Reader
  if ( Serial.available() ) {
    Serial.read();
  }
}
```

Das geänderte Programm kann als "NMEA2000-BME280-Teil2" gespeichert werden.
Mit "Sketch" und "Hochladen" kann das erweiterte Programm nun hochgeladen und gestartet werden.

Falls noch Fehler im geänderten eigenen Programm enthalten sind, bitte das eigene Programm mit dem Programm [NMEA2000-BME280-2.ino](https://github.com/AK-Homberger/NMEA2000-Workshop/blob/main/Software/NMEA2000-BME280-2/NMEA2000-BME280-2.ino) vergleichen.

Falls es funktioniert hat, sollte der NMEA-Reader nun folgendes anzeigen:

![NMEA-Reader2](https://github.com/AK-Homberger/NMEA-Workshop/blob/main/Bilder/NMEAReader-2.png)


So einfach ist es, das Beispielprogram mit weiteren Sendedaten zu erweitern.

# Workshop-Aufgabe: Erweiterung um Luftdruck

Kommen wir nun zur ersten Workshop-Aufgabe: Es soll eigenständig der Luftdruck zum Beipielprogramm hinzugefügt werden.

Folgende Rahmenbedingungen sollen gelten:

- Der neue Offset soll "PressureSendOffset" heißen.
- Die neue Funktion soll den Namen "SendN2kPressure()" haben.
- Die Luftfeuchtigkeit wird mit "Pressure = bme.readPressure() /100;" im mBar gemessen und im Seriellen Monitor ausgegeben.
- Der Luftdruck soll als PGN130314 jewiels 50 ms nach der Luftfeuchtigkeit gesendet werden.
- Als Typ soll "Atmosphärischer Druck" angegeben werden.

Viel Erfolg beim Zusammensuchen der relevanten Informationen aus den Header-Dateien und beim Erweitern des neuen Beispielprogramms.

Einfach die oben beschriebenen Schritte erneut ausführen. Nur diesmal für den neuen Luftdruck-Wert.

Die Auflösung erfolgt [hier](https://github.com/AK-Homberger/NMEA-Workshop/blob/main/Docs/BME280-3.md).


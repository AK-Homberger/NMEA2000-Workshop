# NMEA2000-Thermometer, Hygrometer und Barometer mit BME280

Das [BME280-Modul](https://www.bosch-sensortec.com/media/boschsensortec/downloads/datasheets/bst-bme280-ds002.pdf) von Bosch ist ein Multi-Sensor-Modul, das Temperatur, Luftfeuchte und barometrischen Druck messen kann.
Für die Beispiele mit dem BME280, ist das Steckbrett mit dem BME280-Modul zu erweitern. 

**Um Schäden durch falsche Steckverbindungen zu vermeiden, bitte den ESP32 immer zuerst von der USB-Stromversorgung trennen, bevor Steckverbindungen gesteckt oder geändert werden.**

Das sollte dann so aussehen:

![Steckbrett mit BME280](https://github.com/AK-Homberger/NMEA-Workshop/blob/main/Bilder/NMEA2000-BME280_Steckplatine.png)

Wir nutzen den BME280 mit dem [I2C-Bus](https://de.wikipedia.org/wiki/I%C2%B2C). Dazu ist 3,3 Volt, GND und SDA (GPIO 21) und SCL (GPIO 22) zu verbinden. Wenn das verwendete Modul etwas anders aussieht, macht das nichts. Es sind unterschiedliche Module im Umlauf.

Wenn das BME280-Board einen herausgeführten CSB-Anschluss hat, diesen bitte mit 3,3 Volt verbinden. Das wählt dann den I2C-Bus aus (anstatt [SPI](https://de.wikipedia.org/wiki/Serial_Peripheral_Interface)).

Falls das Modul einen SDO-Anschluss hat, bitte diesen mit GND verbinden. Damit legen wir die I2C-Adresse 0x76 fest.

## Bibliothek für das BME280-Modul installieren
Zur Nutzung des BME280-Moduls müssen wir nun noch die Adafruit-BME-280-Bibliothek installieren. Das geht recht einfach, mit dem Arduino-IDE Bibliotheksverwalter.

Hierzu im Menü "Sketch", "Bibliothek einbinden" und dann "Bibliotheken verwalten..." auswählen. Das startet den Bibliotheksverwalter. In das Suchfeld "BME280" eintragen, dann "Adafruit BME280 Library" auswählen und "Installieren" drücken.


## Beispielprogramm installieren
Nun installieren wir das erste Beispielprogramm auf dem **rechten** ESP32. Dazu wird der rechte ESP32 mit USB verbunden und in der Arduino-IDE der neue serielle USB-Port ausgewählt. Dann in das Verzeichnis "NMEA2000-BME280-1" des heruntergeladenen und entpackten Repositories wechseln und auf [NMEA2000-BME280-1.ino](https://github.com/AK-Homberger/NMEA2000-Workshop/blob/main/Software/NMEA2000-BME280-1/NMEA2000-BME280-1.ino) doppelklicken.

Das Beispielprogramm ist nun in der Arduino-IDE geladen. Um es auf den ESP32 zu übertragen und zu starten, wählen wir im Menü "Sketch" und dann "Hochladen". Das Programm wird nun übersetzt (kompiliert) und dann auf den ESP32 übertragen. 

Nun wollen wir sehen, ob alles funktioniert. Dazu wählen wir im Menü "Werkzeuge" und dann "Serieller Monitor". Die Baudrate (rechts unten im Fenster) setzen wir auf "115200 Baud".

Jetzt sollten wir mehrere Zeilen sehen, in denen die Temperatur angezeigt wird: **"09:46:27.022 -> Temperature: 20.7 °C"**

Falls keine Temperatur angezeigt wird, ist irgendwo ein Fehler passiert. Entweder in der Verkabelung oder das BME280-Modul nutzt eine abweichende Adresse für den I2C-Bus.

Beides müssen wir dann kontrollieren und beheben. 

**Bei Änderung der Beschaltung des BME280 bitte immer kurz den USB-Anschluss abziehen. Der BME280 wertet die Signale CSB und SDO nur beim Start, nach dem Anlegen der Versorgungsspannung aus.**

Falls die Verkabelung OK ist, suchen wir im Programm die Zeile 63:

```
if (!bme.begin(0x76)) {
````

Den Wert "0x76" ersetzen wir dann mit "0x77". Danach den Sketch speichern und erneut hochladen.
Je nach Beschaltung auf der Platine, kann ein BME280 eine von beiden Adressen haben. 

Spätestens jetzt sollte im Seriellen Monitor die Temperatur angezeigt werden.

## Anzeige der NMEA2000-Daten mit dem NMEA-Reader
Im nächsten Schritt wollen wir sehen, ob unser kleines NMEA2000-Netzwerk auch funktioniert.

Mit dem NMEA-Reader können wir Daten vom NMEA2000-Netzwerk anzeigen. Dazu starten wir das zuvor installierte Programm "NMEAReader"
Im Reader wählen wir oben den zuvor notierten seriellen Port vom **linken** ESP32 aus und setzen auch hier die Baudrate auf 115200 Baud.

Die Anzeige sollte dann in etwa so aussehen:

![NMEAReader-1](https://github.com/AK-Homberger/NMEA-Workshop/blob/main/Bilder/NMEAReader-1.png)

Eventuell ist beim ersten Mal die PGN-Zeile anzuwählen und rechts im Fenster der Reiter "Details" auszuwählen.

Wenn die Anzeige so aussieht, dann erst einmal **"Herzlichen Glückwunsch"!"** Es funktioniert alles. 

Die Daten werden vom rechten Modul korrekt gesendet, unser NMEA2000-Bus funktioniert, und das linke Modul hat die Daten korrekt empfangen. Der dort installierte (ActisenseListenerSender-ESP32) Sketch nimmt die Daten vom NMEA2000-Bus auf und sendet sie über die serielle Schnittstelle an das Programm NMEA-Reader. Das Programm dekodiert die Daten und zeigt sie an.

Doch was bedeutet die Anzeige?

- Wir sehen, dass PGN130312 empfangen wurde. Der Name ist "Temperature - DEPRECATED".
- Wir sehen auch die Uhrzeit und auch das Sendeinterval 1.00 (= 1 mal pro Sekunde).
- Es wurde die Quell-Adresse (SRC) "34" vom sendenden Modul genutzt.
- Im rechten Fenster sehen wir Details. So wird in Feld 4 die Temperatur angezeigt (in Kelvin und auch in Grad Celsius).

## Aufbau von PGN-Nummern und Adressierung
Im obigen Beispiel wird rechts im Fenster die PGN-Nummer auch als hexadezimale Zahl (0x1FD08) angezeigt. Auch wird die Zieladresse (Destination) mit 255 angegeben. Das bezeichnet eine Broadcast-Zieladresse. Das bedeutet, dieses PGN ist an alle Busteilnehmer gerichtet.

Im Gegensatz dazu gibt es auch PGNs, die an einzelne Zieladressen gesendet werden können. Welche PGNs Broadcast-PGNs sind, und welche auch an einzelne Zieladressen gesendet werden können, kann man leicht selbst herausfinden.

Man gibt dazu einfach die [PGN-Nummer](https://www.nmea.org/Assets/20151026%20nmea%202000%20pgn_website_description_list.pdf) als Dezimalzahl in den Windows-Taschenrechner ein (vorher den Modus "Programmierer" im Tschenrechner wählen). Dann wählt man die Darstellung HEX.

Für unser obiges Beispiel 130312 ergibt sich dann "1FD08". Wichtig für die Unterscheidung, Broadcast oder nicht, ist das zweite Byte von rechts, also die beiden Ziffern nach der "1". Ist diese Zahl kleiner F0 (dezimal 240), dann handelt es sich um ein adressierbares PGN. Ist die Zahl größer oder gleich F0, dann ist es ein Broadcast-PGN.

Für unser PGN 130312 ist das Byte "FD", also ist es größer als F0. Dieses PGN ist daher ein Broadcast-PGN und kann **ausschließlich** an alle Busteilnehmer gesendet werden.

Geben wir dagegen die PGN-Nummer 059392 "ISO Acknowledgment" in den Taschenrechner ein, zeigt sich folgender hexadezimaler Wert "E800". "E8" ist kleiner als F0, daher ist dieses PGN adressierbar. Adressierbare PGNs enden auch immer auf "00".

In der von der NMEA gewählten dezimalen Schreibweise der PGN-Nummern sieht man den PGNs leider nicht direkt an, ob sie adressierbar sind oder nicht.

Hintergrundinformationen zum Aufbau der PGN-Nummern und zur Adressierung sind auch in der Beschreibung zu [SAEJ1939](https://assets.vector.com/cms/content/know-how/_application-notes/AN-ION-1-3100_Introduction_to_J1939.pdf) zu finden. NMEA2000 basiert ja auf dem Standard zu Nutzfahrzeugen. Dank der restriktiven NMEA2000-Dokumentenlage gibt es keine vergleichbare, frei verfügbare Beschreibung zu NMEA2000 PGNs.

Alle in diesem Workshop verwendeten PGNs sind übrigens, wie überhaupt die meisten PGNs, Broadcast-PGNs.

**Tip: Der ESP32 mit ActisenseListenerSender-ESP32.ino kann zusammen mit dem NMEA-Reader sehr gut zur Fehlersuche auf dem Boot eingesetzt werden.**

# Struktur des Beispielprogramms

Schön das es funktioniert hatte. Aber wie hat das Beispielprogramm das nun gemacht?

Das Beispielprogramm ist in der Programmierprache C und C++ geschrieben. Eine guter Einstieg speziell zu C und Arduino ist [hier](http://manuel-jasch.de/post/einf%C3%BChrung-in-die-programmierung-mit-c-und-arduino/) zu finden. Für C++ eignet sich diese [Seite](https://www.w3schools.com/cpp/default.asp) ganz gut.

Der Aufbau unserer Beispielprogramme in der Arduino-IDE ist im Prinzip immer gleich.

1. Include-Dateien (#include) für verwendete Komponenten und Bibliotheken.
2. Definition von Konstanten (#define).
3. Deklaration/Definition von globalen Variablen/Objekten.
4. Funktion "setup()" zur Initialisierung (Baudrate usw.). Diese Funktion wird einmalig beim Start des Programms ausgeführt.
5. Notwendige Hilfsfunktionen.
6. Funktion "loop()". Diese Funktion wird als Schleife immer wieder ausgeführt.

Kommen wir nun zum Inhalt unseres ersten Beispielprogramms:

## 1./2. Include-Dateien und Definition von Konstanten

```
#define ESP32_CAN_TX_PIN GPIO_NUM_5  // Set CAN TX port to 5 
#define ESP32_CAN_RX_PIN GPIO_NUM_4  // Set CAN RX port to 4

#include <Arduino.h>
#include <Preferences.h>
#include <NMEA2000_CAN.h>  // This will automatically choose right CAN library and create suitable NMEA2000 object
#include <N2kMessages.h>

// Include files for BME280
#include <Wire.h>
#include <Adafruit_Sensor.h>
#include <Adafruit_BME280.h>

// Set time offsets
#define SlowDataUpdatePeriod 1000  // Time between CAN Messages sent
#define TempSendOffset 0
```
Als Erstes sehen wir hier die Definition zweier Konstanten, welche die GPIO-Ports für CAN TX und CAN RX festlegen. Die Ports 5 und 4 sind die Ports, die wir auch auf dem Steckbrett verbunden haben. Die Definition muss hier vor den Include-Dateien für die NMEA2000-Bibliotheken erfolgen, da die Werte dort zur Konfiguration genutzt werden.

- Die oberen Include-Dateien sind für die generelle Funktion (Arduino.h) und für die Nutzung des permanenten Speichers des ESP32 notwendig (Preferences.h).
- Die folgenden beiden sind für die Nutzung der NMEA-Bibliotheken notwendig.
- Die folgenden drei Include-Dateien sind für die Nutzung der BME2800-Bibliothek notwendig.
- Mit "SlowDataUpdatePeriod 1000" wird das Interval für das Senden festgelegt (1000 = 1000ms = 1 Sekunde).
- Mit "TempSendOffset" wird der Abstand der einzelenen zu sendene PGNs festgelegt (hier 0 ms). Der Sinn wird später deutlich, wenn wir auch die Luftfeuchtigkeit senden werden.

## 3. Definition von globalen Variablen/Objekten

Kommen wir nun zur Definition von globalen Variablen und Objekten. Globale Variablen heißen "global", weil sie in allen Funktionen verwendet werden können. Im Gegensatz zu "globalen" sind "lokale" Variablen, die in Funktionen definiert werden, nur in der jeweiligen Funktion bekannt und nutzbar.

```
Adafruit_BME280 bme; // I2C

int NodeAddress;            // To store last Node Address
Preferences preferences;    // Nonvolatile storage on ESP32 - To store LastDeviceAddress

// Set the information for other bus devices, which messages we support
const unsigned long TransmitMessages[] PROGMEM = {130312L, // Temperature
                                                  0
                                                 };
```

Als Erstes wird ein Objekt "bme" vom Typ "Adafruit_BME280" erstellt. Diese Objekt enthält die notwendigen Methoden/Funktionen zur Verwendung des BME280-Moduls.

Die nächsten beiden Zeilen sind für die Verwaltung der eigenen Node-Adresse notwendig. Dazu kommen wir später noch.

Mit "TransmitMessages[]" wird eine Liste mit PGNs angelegt, die vom Modul gesendet werden. Die Liste enthält erst einmal nur den PGN 130312. Später kommen weitere hinzu.

## 4. Funktion setup()

```
void setup() {
  uint8_t chipid[6];
  uint32_t id = 0;
  int i = 0;

  // Init USB serial port
  Serial.begin(115200);
  delay(10);
```
Die Funktion setup() wird einmalig beim Start des Programms ausgeführt.
Als Erstes werden lokale Variablen definiert, die in setup() zur Berechnung der eindeutigen Modul-ID genutzt werden.

Als Nächstes wird die serielle Schnittstelle (USB) mit der Baud-Rate 115200 gestartet. Das ist notwendig, um den Seriellen Monitor der Arduino-IDE nutzen zu können.

```
 // Init BME280 I2C address depends on sensor 0x76 or 0x77.

  if (!bme.begin(0x76)) {
    Serial.println("Could not find a valid BME280 sensor, check wiring!");
    while (1) {}
  }

```
Dann wird das BME280-Modul initialisiert und gestartet. In bme.begin() wird die genutzte I2C-Adresse mit angegeben. Je nach verwendetem Modul kann es notwendig sein, die Adresse zu ändern. Wenn der Start nicht (! = logisch nicht Wahr) erfolgreich war, wird über die serielle Schnittstelle eine Fehlermeldung ausgegeben und das Programm gestoppt (while (1) {}).

```
  // Reserve enough buffer for sending all messages.
  NMEA2000.SetN2kCANMsgBufSize(8);
  NMEA2000.SetN2kCANReceiveFrameBufSize(150);
  NMEA2000.SetN2kCANSendFrameBufSize(150);
```
Im nächsten Schritt reservieren wir genügend Pufferspeicher zur Bearbeitung der CAN-Nachrichten und PGNs. 

```
  // Generate unique number from chip id
  esp_efuse_read_mac(chipid);
  for (i = 0; i < 6; i++) id += (chipid[i] << (7 * i));
```
Mit den obigen Zeilen wird eine eindeutige "id" aus der WLAN-Netzwerkadresse (MAC = Media Access Control) des ESP32-Moduls berechnet.

**Hinweis:** Die in den Beispielen verwendete Funktion "esp_efuse_read_mac()" wurde von Espressiv umbenannt (s. Warnung im der Compilerausgabe). Zukünftig soll der Funktionsname "esp_efuse_mac_get_default()" genutzt werden. Der aktuelle Funktionsname wird noch einige Zeit funktionieren aber in ein einer späteren Version eventuell gelöscht.

```
  // Set product information
  NMEA2000.SetProductInformation("1", // Manufacturer's Model serial code
                                 100, // Manufacturer's product code
                                 "My Sensor Module",  // Manufacturer's Model ID
                                 "1.0.2.25 (2019-07-07)",  // Manufacturer's Software version code
                                 "1.0.2.0 (2019-07-07)" // Manufacturer's Model version
                                );
```
Jedes regelkonforme Modul am MNEA2000-Bus muss Produkt- und Geräte-Informationen über sich selbst bereitstellen.

Die konkreten Inhalte der Produkt-Information sind für die korrekte Funktion nicht relevant. Sie dienen nur der Information und können auf anderen Geräten (z.B. NMEA2000-Plotter) angezeigt werden. Im NMEA-Reader werden diese Informationen übrigens im Reiter "Properties" angezeigt. Allerdings nur, wenn das ESP32 Modul nach Anschluss des Readers neu gestartet wurde (RST-Taste) und der PGN 60928 "ISO Address Claim" vom Modul gesendet wurde.

```
// Set device information
  NMEA2000.SetDeviceInformation(id, // Unique number. Use e.g. Serial number.
                                132, // Device function=Analog to NMEA 2000 Gateway. See codes on http://www.nmea.org/Assets/20120726%20nmea%202000%20class%20&%20function%20codes%20v%202.00.pdf
                                25, // Device class=Inter/Intranetwork Device. See codes on  http://www.nmea.org/Assets/20120726%20nmea%202000%20class%20&%20function%20codes%20v%202.00.pdf
                                2046 // Just choosen free from code list on http://www.nmea.org/Assets/20121020%20nmea%202000%20registration%20list.pdf
                               );

```
Als Nächstes werden die Geräte-Informationen festgelegt. In folgender Reihenfolge:

- Die berechnete eindeutige ID.
- Geräte-Funktion: 132 entspricht einem Analog zu NMEA2000-Gateway.
- Geräte-Klasse: 25 entspricht einem "Inter/Intranetwork Device".
- Freier Hersteller-Code. Wir nutzen hier 2046.

Wir hätten auch die Kombination 75/130 für einen Temperatursensor nutzen können. Da wir aber noch andere Werte wie Luftfeuchte und Druck hinzufügen wollen, ist dies laut Standard nicht möglich. Das hätte individuelle NMEA2000-Nodes erfordert. Warum auch immer.

Die zu verwendenden Codes sind auf der [NMEA.Org-Webseite](http://www.nmea.org/Assets/20120726%20nmea%202000%20class%20&%20function%20codes%20v%202.00.pdf) zu finden.

Für andere Geräte (zum Beispiel für ein WLAN-Gateway) müssen auch andere Codes genutzt werden.

```
  preferences.begin("nvs", false);                          // Open nonvolatile storage (nvs)
  NodeAddress = preferences.getInt("LastNodeAddress", 34);  // Read stored last NodeAddress, default 34
  preferences.end();
  Serial.printf("NodeAddress=%d\n", NodeAddress);
```

Im nächsten Schritt setzen wir die Node-Adresse unseres Moduls. Als Standard-Adresse legen wir die Nummer 34 fest. Falls es zuvor einen Adresskonflikt gegeben hatte (zwei Geräte wollten die gleiche Adresse 34 nutzen), handeln beide Geräte eindeutige Adressen aus. Es gehört zum NMEA2000-Standard, dass alle Geräte sich die letzte ausgehandelte Adresse merken und beim nächsten Start auch verwenden, um einen erneuten Adresskonflikt zu vermeiden.

Der ESP32 hat einen nichtflüchtigen Speicher (auch Flash-Memory oder EEPROM genannt). Aus diesem Speicher lesen wir einen zuvor gegebenenfalls gespeicherten Wert für "LastNodeAddress" und weisen diesen der globalen Variablen "NodeAddress" zu. Wir werden später bei der Funktion CheckSourceAddressChange() noch einmal darauf zurückkommen.

```
  // If you also want to see all traffic on the bus use N2km_ListenAndNode instead of N2km_NodeOnly below
  NMEA2000.SetMode(tNMEA2000::N2km_NodeOnly, NodeAddress);
  NMEA2000.ExtendTransmitMessages(TransmitMessages);
```

Im nächsten Schritt setzen wir den Geräte-Modus und die gerade ermittelte Node-Adresse. Der Geräte-Modus legt generelle Funktionsweisen des Moduls fest. Details sind sind in der [NMEA2000-Bibliotheks-Referenz](https://github.com/ttlappalainen/NMEA2000/blob/master/Documents/NMEA2000_library_reference.pdf) nachzulesen.

Für unseren Temperatur-Sensor ist "N2km_NodeOnly" die richtige Wahl, da uns die anderen Informationen auf dem Bus im Moment nicht interessieren.

Mit "ExtendTransmitMessages" erweitern wir die zu sendenden PGNs mit der weiter oben festgelegten Liste "TransmitMessages".

```
  NMEA2000.Open();

  delay(200);
```
Als Letztes in der Funktion setup() starten wir die die NMEA2000-Bibliothek und warten kurz (200 ms).

## 5. Notwendige Hilfsfunktionen

Als Nächstes sehen wir uns die Hilfsfunktionen etwas genauer an.

Wir starten mit den Funktionen zur Steuerung des Sende-Intervalls:

```
// Functions to control send interval and time offets

//*****************************************************************************
bool IsTimeToUpdate(unsigned long NextUpdate) {
  return (NextUpdate < millis());
}


//*****************************************************************************
unsigned long InitNextUpdate(unsigned long Period, unsigned long Offset = 0) {
  return millis() + Period + Offset;
}


//*****************************************************************************
void SetNextUpdate(unsigned long & NextUpdate, unsigned long Period) {
  while ( NextUpdate < millis() ) NextUpdate += Period;
}

```
Für ungeübte C-Programmierer sind die Funktionen wohl recht schwer zu verstehen. Wir gehen hier auch nicht im Detail darauf ein. Wir werden aber in der nächsten Funktion sehen, wie sie zu nutzen sind.

```
void SendN2kTemperature(void) {
  static unsigned long SlowDataUpdated = InitNextUpdate(SlowDataUpdatePeriod, TempSendOffset);
  tN2kMsg N2kMsg;
  double Temperature;

  if ( IsTimeToUpdate(SlowDataUpdated) ) {
    SetNextUpdate(SlowDataUpdated, SlowDataUpdatePeriod);

    Temperature = bme.readTemperature();
    Serial.printf("Temperature: %3.1f °C \n", Temperature);

    // Definition from N2kMessages.h
    // void SetN2kPGN130312(tN2kMsg &N2kMsg, unsigned char SID, unsigned char TempInstance, tN2kTempSource TempSource,
    //        double ActualTemperature, double SetTemperature=N2kDoubleNA);

    // tN2kTempSource is defined in N2kTypes.h

    // Set N2K message
    SetN2kPGN130312(N2kMsg, 0, 0, N2kts_MainCabinTemperature, CToKelvin(Temperature), N2kDoubleNA);

    // Send message
    NMEA2000.SendMsg(N2kMsg);
  }
}
```
Mit der obigen Funktion SendN2kTemperature(void) bauen wir die zu sendenden NMEA2000-Daten zusammen und senden sie auf den NMEA2000-Bus.

Als Erstes werden drei lokale Variablen definiert:

1. "static unsigned long SlowDataUpdated" wird definiert und durch einmaliges "InitNextUpdate(SlowDataUpdatePeriod, TempSendOffset)" mit "millis() + Period + Offset" initialisiert. Da die Variable als "static" definiert ist, behält sie ihren Wert auch über mehrere Funktionsaufrufe hinweg.
2. Mit "tN2kMsg N2kMsg;" wird eine lokale Variable N2kMsg definiert. Sie kann eine NMEA2000-Nachricht enthalten.
3. Mit "double Temperature;" wird ein lokale Variable Temperatur definiert.

Dann wird geschaut, ob es Zeit für das nächste Senden ist.
Mit SetNextUpdate(SlowDataUpdated, SlowDataUpdatePeriod); wird gleich der nächste Update-Zeitpunkt definiert.

Mit "Temperature = bme.readTemperature();" lesen wir die aktuelle Temperatur vom BME280-Modul (in Grad Celsius).

Mit "Serial.printf("Temperature: %3.1f °C \n", Temperature);" geben wir die Temperatur formatiert an die serielle Schnittstelle aus. Informationen zur Formatierung mit printf() sind [hier](http://www.cplusplus.com/reference/cstdio/printf/) zu finden.

Mit "SetN2kPGN130312(N2kMsg, 0, 0, N2kts_MainCabinTemperature, CToKelvin(Temperature), N2kDoubleNA);" bauen wir die NMEA2000-Nachricht zusammen und mit "NMEA2000.SendMsg(N2kMsg);" senden wir die Nachricht auf den NMEA2000-Bus.

Die verschiedenen PGNs und eventuell nötige Datentypen sind in den beiden Dateien ["N2kMessages.h"](https://github.com/ttlappalainen/NMEA2000/blob/master/src/N2kMessages.h) und ["N2kTypes.h"](https://github.com/ttlappalainen/NMEA2000/blob/master/src/N2kTypes.h) festgelegt.

Für das hier verwendete PGN130312 ([Zeile 1334](https://github.com/ttlappalainen/NMEA2000/blob/db22adbb3fec182ecae2ae8bc816378ac43d7fc3/src/N2kMessages.h#L1334)) werden folgende Argumente erwartet:

1. "tN2kMsg &N2kMsg": Das ist die zuvor definierte Variable zum Speichern der NMEA2000-Nachricht.
2. "unsigned char SID": Die Sequence ID ermöglicht die Kennzeichnung zusammengehöriger Nachrichten. Das benötigen wir hier nicht und setzen ein "0" ein.
3. "unsigned char TempInstance": Die Temperaturinstanz kennzeichnet die Quelle der Temperatur. Da wir nur einen Temperatursensor haben, setzen wir hier eine "0" ein.
4. "tN2kTempSource TempSource": Hier können wir den Typ der Temperaturquelle benennen. Die möglichen Typen sind in "N2kTypes.h" definiert. Wir nutzen hier "N2kts_MainCabinTemperature", für die Kabinentemperatur.
5. "double ActualTemperature": Hier wird die aktuelle Temperatur in Kelvin eingetragen. Da unsere Temperatur vom BME280 in Grad Celsius vorliegt, nutzen wir das Makro "CToKelvin(Temperature)" zur Umwandlung.
6. "double SetTemperature": Falls man keine aktuelle Temperatur senden, sondern eine Temperatur einstellen möchte (zum Beispiel für ein Heizungsthermostat), kann man die Temperatur hier angeben. Da wir das nicht nutzen setzen wir hier "N2kDoubleNA". 

Die NMEA2000-Nachricht ist nun erfolgreich zusammengesetzt.
Sie kann mit "NMEA2000.SendMsg(N2kMsg);" nun an den NMEA2000-Bus gesendet werden.

NMEA2000 erwartet übrigens alle Werte konform zu den definierten [SI-Einheiten](https://de.wikipedia.org/wiki/Internationales_Einheitensystem).
Das bedeutet Temperatur in Kelvin, Geschwindigkeit in m/s. Winkel in Radiant (rad) und Druck in Pascal usw. Hilfreiche Umrechnungen sind in der Datei [N2kMessages.h](https://github.com/ttlappalainen/NMEA2000/blob/db22adbb3fec182ecae2ae8bc816378ac43d7fc3/src/N2kMessages.h#L42) definiert.

Andere PGNs werden wir in späteren Beispielen verwenden.

Kommen wir nun zur vorletzten Funktion in diesem Beispiel:

```
void CheckSourceAddressChange() {
  int SourceAddress = NMEA2000.GetN2kSource();

  if (SourceAddress != NodeAddress) { // Save potentially changed Source Address to NVS memory
    NodeAddress = SourceAddress;      // Set new Node Address (to save only once)
    preferences.begin("nvs", false);
    preferences.putInt("LastNodeAddress", SourceAddress);
    preferences.end();
    Serial.printf("Address Change: New Address=%d\n", SourceAddress);
  }
}
```

Wie bereits bei den Variablen beschrieben, muss jedes NMEA2000-Konformes Gerät auf Adresskollisionen reagieren können.
Das machen wir mit der Funktion CheckSourceAddressChange(). Hier prüfen wir, ob sich die eigene Node-Adresse aufgrund eines Adresskonflikts geändert hat (SourceAddress != NodeAddress). Falls sich die Adresse geändert hatte, speichern wir die neue Adresse im Flash-Speicher des ESP32. Beim nächsten Start des Moduls wird die geänderte Adresse dann auch gleich wieder verwendet, um einen erneuten Adresskonflikt zu vermeiden.

## 6. Funktion loop()
Die letzte Funktion ist die Funktion loop(), die automatisch und wiederkehrend immer wieder aufgrufen wird.

```
void loop() {

  SendN2kTemperature();

  NMEA2000.ParseMessages();

  CheckSourceAddressChange();

  // Dummy to empty input buffer to avoid board to stuck with e.g. NMEA Reader
  if ( Serial.available() ) {
    Serial.read();
  }
}
```

1. Mit SendN2kTemperature() stoßen wir regelmäßig das Senden der Temperatur an. Ob wirklich gesendet wird, findet die Funktion selbt heraus, auf Basis der definierten Offsets.
2. Mit NMEA2000.ParseMessages() geben wir der NMEA2000-Bibliothek regelmäßig die Gelegenheit, auf empfangene Nachrichten zu reagieren.
3. Mit CheckSourceAddressChange() prüfen wir regelmäßig, ob sich die eigene Adresse geändert hat.
4. Dann lesen wir eventuell empfangene Zeichen aus der seriellen Schnittstelle, um Problemen vorzubeugen.

Hier noch ein Hinweis: In anderen Arduino-Programmen sieht man häufig die Verwendung von delay() um einen Zeitraum abzuwarten. Bei NMEA2000-Programmen ist es keine gute Idee, in loop() oder daraus aufgerufenen Funktionen, delay() zu verwenden. Das würde den regelmäßigen Aufruf von NMEA2000.ParseMessages() verzögern, und kann zu Problemen mit der zügigen Verarbeitung der Nachrichten führen.

Das war es mit dem ersten Beispielprogramm. 

Im nächsten Schritt werden wir das BME280-Beipielprogramm erweitern, um auch die Luftfeuchtigkeit mit einem eigenen PGN zu senden.

BME280 [Teil 2](https://github.com/AK-Homberger/NMEA2000-Workshop/blob/main/Docs/BME280-2.md)
